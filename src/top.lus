open Interface

-- Enables to only declare the io once.
node declare_once(port: int; direction: int) returns(out: int)
var i: bool;
let
   i = true fby false;
   out = if i then declare_io(port, direction) else 0;
tel;

node count_true() returns (y: int);
let
   y = (0 fby y) + 1;
tel;

node led(period: int; pin: int; toggle: bool) returns (out: bool)
var i : int;
   statee : bool;
   change : bool;
   s : int;
let
   s = declare_once(pin, 1);
   change = (modulo(count_true(), period) = 0);
   statee = true -> 
      if toggle then (if change then (not (pre statee)) else (pre statee)) else false;
   i = s ->
      (if change
      then change_pin_state(pin, statee)
      else 0);
   out = if change
      then not (false fby out) 
      else (false fby out);
tel;

node xor_node(i_1, i_2: bool) returns (o: bool)
let
   o = (i_1 and not i_2) or (i_2 and not i_1);
tel;

node toggle_node(inp: bool) returns (outp: bool);
var last_val: bool;
let
   last_val = false -> pre(inp);
   outp = false -> xor_node((pre outp), (inp and (not last_val)));
tel;

node led_control(pin: int; statee: bool) returns (out: bool)
var i : int;
   s : int;
let
   s = declare_once(pin, 1);
   i = change_pin_state(pin, statee);
   out = true;
tel;



-- Hardware setup :
-- LED on pins 2, 3, 4, 6 (configured as OUTPUT)
-- Button on pin 5 (configured as INPUT)
node main() returns (o: bool);
var i: bool;
   j: int;
   k: bool;
   m: bool;
   on_off: bool;
let
   j = declare_once(5, 0);
   on_off = toggle_node(read_pin_state(5));
   i = led(1, 2, on_off);
   k = led(5, 3, true);
   m = led_control(6, on_off);
   o = led_control(4, read_pin_state(5));
tel;